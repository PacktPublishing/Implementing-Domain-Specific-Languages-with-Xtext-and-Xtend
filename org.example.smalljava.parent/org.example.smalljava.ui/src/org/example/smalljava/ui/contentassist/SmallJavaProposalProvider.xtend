/*
 * generated by Xtext 2.10.0
 */
package org.example.smalljava.ui.contentassist

import com.google.inject.Inject
import org.eclipse.emf.ecore.EObject
import org.eclipse.jface.viewers.StyledString
import org.eclipse.xtext.Assignment
import org.eclipse.xtext.CrossReference
import org.eclipse.xtext.ui.editor.contentassist.ContentAssistContext
import org.eclipse.xtext.ui.editor.contentassist.ICompletionProposalAcceptor
import org.example.smalljava.SmallJavaModelUtil
import org.example.smalljava.smallJava.SJClass
import org.example.smalljava.smallJava.SJMember
import org.example.smalljava.validation.SmallJavaAccessibility

/**
 * See https://www.eclipse.org/Xtext/documentation/304_ide_concepts.html#content-assist
 * on how to customize the content assistant.
 */
class SmallJavaProposalProvider extends AbstractSmallJavaProposalProvider {
	@Inject extension SmallJavaModelUtil
	@Inject extension SmallJavaAccessibility

	override getStyledDisplayString(EObject element, String qualifiedNameAsString, String shortName) {
		if (element instanceof SJMember) {
			new StyledString(element.memberAsStringWithType).append(
				new StyledString(" - " + (element.eContainer as SJClass).name, StyledString.QUALIFIER_STYLER))
		} else
			super.getStyledDisplayString(element, qualifiedNameAsString, shortName)
	}

	override void completeSJSelectionExpression_Member(EObject model, Assignment a, ContentAssistContext context,
			ICompletionProposalAcceptor acceptor) {
		lookupCrossReference(a.getTerminal() as CrossReference, context, acceptor) [ description |
			(description.getEObjectOrProxy as SJMember).isAccessibleFrom(model)
		]
	}
}
